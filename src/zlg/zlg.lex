%{

#include <cstdio>
#include <cstdlib>
#include <zlg.h>
#include <zlgy.gen.hpp>  /* Generated by bison. */

%}

%option noyywrap reentrant
%option bison-bridge bison-locations

%x INLINE

%%

<INITIAL>{

\%                   { BEGIN(INLINE);}
\(                   { return '(';}
\)                   { return ')';}
\[                   { return '[';}
\]                   { return ']';}
\{                   { return '{';}
\}                   { return '}';}
\*                   { return '*'; }
\/                   { return '/'; }
\n                   { return '\n'; }
_                    { return ZPREV; }
[+-]                 { return yytext[0]; }
fun                  { return ZFUN; }
end                  { return ZEND; }
result               { return ZRESULT; }
byte                 { return ZBYTE; }
short                { return ZSHORT; }
long                 { return ZLONG; }
quad                 { return ZQUAD; }
print                { return ZPRINT; }
[[:digit:]]+         { yylval->value = atoi(yytext); return ZNUMBER;}
[a-zA-Z][a-zA-Z0-9]* { yylval->text.assign(yytext); return ZSTRING;}
[$][a-zA-Z0-9]       { yylval->text.assign(yytext); return ZREG;}
[ \t]                {}
.                    {}

}

<INLINE>{

\%                   { BEGIN(INITIAL); }
[^%]*                { yylval->text.assign(yytext); return ZINLINE; }

}


%%

void yyerror(YYLTYPE* loc, void* scanner, zlg::ast& root, const char * err){
    fprintf(stderr, "%d: %s: %s", loc->first_line, "ERROR", err);
    exit(1);
}