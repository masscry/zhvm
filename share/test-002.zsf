################################################################################
#                 FIBBONACHI NUMBERS SEQUENCE                                  #
################################################################################

!data                                              # DATA SEGMENT
!initial                                           # REQUESTED FIB ELEMENT
!30                                                # AS BYTE 30

!code
$s = add[,1023]                                    # SET STACK
$p = add[,@main]                                   # GO TO MAIN

!fib                                               # CALCULATE FIB NUMBERS
$s = sub[$s, 4] $s = svl[$a]                       # SAVE RA TO STACK
$s = sub[$s, 4] $s = svl[$0]                       #      R0
$s = sub[$s, 4] $s = svl[$1]                       #      R1
$s = sub[$s, 4] $s = svl[$2]                       #      R2

$0 = eq[$a, 0] $p = cmn[$0, @fbcase0]              # IF RA == 0 GOTO FBCASE0
$0 = eq[$a, 1] $p = cmn[$0, @fbcase1]              # IF RA == 0 GOTO FBCASE1
$p = add[,@fbcaset]                                # GOTO FBCASET

!fbcase0                                           # 
$b = add[,0]                                       # RESULT = 0
$p = add[,@fibend]                                 # 

!fbcase1                                           #
$b = add[,1]                                       # RESULT = 1
$p = add[,@fibend]                                 #

!fbcaset
$1 = sub[$a,1]                                     # R1 = RA - 1
$2 = sub[$a,2]                                     # R2 = RA - 2

$a = add[$1]                                       # CALCULATE FIB-R1
$s = sub[$s, 4] $s = svl[$p, 8]  $p = add[,@fib]   # CALL FIB
$s = add[$s, 4]                                    # FIX STACK
$1 = add[$b]                                       # R1 = RET

$a = add[$2]                                       # CALCULATE FIB-R2
$s = sub[$s, 4] $s = svl[$p, 8]  $p = add[,@fib]   # CALL FIB
$s = add[$s, 4]                                    # FIX STACK
$2 = add[$b]                                       # R2 = RET

$b = add[$1, $2]                                   # RESULT = R1 + R2
$p = add[,@fibend]                                 #

!fibend
$2 = ldl[$s] $s = add[$s, 4]                       # RESTORE R2
$1 = ldl[$s] $s = add[$s, 4]                       # RESTORE R1
$0 = ldl[$s] $s = add[$s, 4]                       # RESTORE R0
$a = ldl[$s] $s = add[$s, 4]                       # RESTORE RA
$p = ldl[$s]                                       # RETURN


!main
$a = ldb[,@initial]                                # CALCULATE REQUESTED FIB ELEMENT
$s = sub[$s, 4] $s = svl[$p, 8]  $p = add[,@fib]   # CALL FIB
$s = add[$s, 4]                                    # FIX STACK
$a = add[$b]                                       #
cll[,0]                                            # PRINT RET

hlt[]                                              # STOP
